ID         = [a-zA-Z_][a-zA-Z0-9_]*  ; (*letter or underscore followed by any letters, numbers or underscores *)
INT        = [0-9]+                  ;
FLOAT      = INT.[0-9]+              ;
STRING     = "\"(?:[^\"\\]|\\.)*\""  ; (* anything between double-quoted strings, minus escape sequences *)
STRING_CH  = "'(?:[^'\\]|\\.)*'"     ; (* same but single quotes *)

KW_DATA   = "data"   ;
KW_MUT    = "mut"    ;
KW_IF     = "if"     ;
KW_ELSE   = "else"   ;
KW_RETURN = "return" ;

KW_LOOP      = "loop"  ;
KW_SKIP      = "skip"  ;
KW_BREAK     = "break" ;
OP_RANGE_INC = "~"     ;
OP_RANGE_EXC = ".."    ;
OP_TARGET    = "=>"    ;

TERMINATOR = "\n" | ";";

  (* rules *)
artifact = stmt* EOF ;

stmt   = if_block 
       | loop 
       | (loop_control | data_decl | assign | expr) TERMINATOR ;


if_condition = KW_IF expr

if_block = if_condition scope if_tail? ;
if_tail = KW_ELSE (if_block | scope) ;

loop = KW_LOOP (ID ':')? loop_body ;

loop_body = scope if_condition?            (* infinite / post-conditional *)
          | if_condition scope             (* conditional *)
          | range_expr ID scope            (* ranged iteration *)
          | fixed_expr ID? scope           (* fixed iteration, expr must evaluate to integer *)
          ;

range_op = OP_RANGE_EXC | OP_RANGE_INC ;

fixed_expr = expr OP_RANGE_INC
           | OP_RANGE_INC expr
           | expr
           ;

range_expr = expr? range_op expr? ;

loop_control = (KW_SKIP | KW_BREAK) (OP_TARGET ID)? if_condition? ;

data_decl   = KW_MUT? KW_DATA ID (':' ID)? ('=' expr)?    ;

assign = ID '=' expr       

scope  = "{" stmt* "}"

return = KW_RETURN expr TERMINATOR ;

expr       = logical                                  ;  
logical    = equality ( ('&&' | '||') equality )*     ;
equality   = comparison (  ('!=' | '==') comparison)* ;
comparison = term ( ('>' | '>=' | '<' | '<=') term)*  ;
term       = factor ( ('-' | '+') factor)*            ;
factor     = unary ( ('/' | '*') unary )*             ;
unary      = ('!' | '-') unary | primary              ;
primary    = grouping | literal | array_literal | ID  ;

grouping   = "(" expr ")" ;
literal    = number
           | string
           | "true"
           | "false"
           | "none"
           ;

array_literal = '[' elem_list? ']'   ;
elem_list = expr (',' expr)* (',')?  ;

number = INT | FLOAT        ;
string = STRING | STRING_CH ;
