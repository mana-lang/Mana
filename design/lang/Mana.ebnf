ID         = [a-zA-Z_][a-zA-Z0-9_]*  ; (*letter or underscore followed by any letters, numbers or underscores *)
INT        = [0-9]+                  ;
FLOAT      = INT.[0-9]+              ;
STRING     = "\"(?:[^\"\\]|\\.)*\""  ; (*anything between double-quoted strings, minus escape sequences *)
STRING_CH  = "'(?:[^'\\]|\\.)*'"     ; (* same but single quotes *)

KW_DATA   = "data"   ;
KW_MUT    = "mut"    ;
KW_IF     = "if"     ;
KW_ELSE   = "else"   ;
KW_RETURN = "return" ;

KW_LOOP      = "loop"  ;
KW_SKIP      = "skip"  ;
KW_BREAK     = "break" ;
OP_RANGE_INC = "~"     ;
OP_RANGE_EXC = ".."    ;
OP_TARGET    = "=>"    ;

TERMINATOR = "\n" | ";";

  (* rules *)
artifact = stmt* EOF ;

stmt   = if_stmt | loop | (decl | assign | expr) TERMINATOR ;

decl   = KW_MUT? KW_DATA ID ('=' expr)?    ;
assign = ID '=' expr                       ;

if_stmt = KW_IF expr scope if_tail? ;
if_tail = KW_ELSE (if_stmt | scope) ;

loop = KW_LOOP (ID ':')? loop_body ;

loop_condition = KW_IF expr

loop_body = scope loop_condition?            (* infinite / post-conditional *)
          | loop_condition scope             (* conditional *)
          | range_expr ID scope              (* ranged iteration *)
          | expr ID? scope                   (* fixed iteration, expr must evaluate to integer *)
          ;

range_expr = expr? OP_RANGE_INC expr?
           | expr? OP_RANGE_EXC expr?
           ;

loop_control = (KW_SKIP | KW_BREAK) (OP_TARGET ID)? ;

scope  = "{" stmt* "}"

return = KW_RETURN expr TERMINATOR ;

expr       = logical                                  ;  
logical    = equality ( ('&&' | '||') equality )*     ;
equality   = comparison (  ('!=' | '==') comparison)* ;
comparison = term ( ('>' | '>=' | '<' | '<=') term)*  ;
term       = factor ( ('-' | '+') factor)*            ;
factor     = unary ( ('/' | '*') unary )*             ;
unary      = ('!' | '-') unary | primary              ;
primary    = grouping | literal | array_literal | ID  ;

grouping   = "(" expr ")" ;
literal    = number
           | string
           | "true"
           | "false"
           | "none"
           ;

array_literal = '[' elem_list? ']'   ;
elem_list = expr (',' expr)* (',')?  ;

number = INT | FLOAT        ;
string = STRING | STRING_CH ;
